// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.28;

import {PortfolioFactory} from "../../../accounts/PortfolioFactory.sol";
import {IVoter} from "../../../interfaces/IVoter.sol";
import {IVotingEscrow} from "../../../interfaces/IVotingEscrow.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {IERC721Receiver} from "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
import {CollateralManager} from "../collateral/CollateralManager.sol";
import {UserMarketplaceModule} from "../marketplace/UserMarketplaceModule.sol";
import {AccessControl} from "../utils/AccessControl.sol";
/**
 * @title VotingEscrowFacet
 * @dev Facet that interfaces with voting escrow NFTs
 */
contract VotingEscrowFacet is AccessControl, IERC721Receiver {
    using SafeERC20 for IERC20;
    PortfolioFactory public immutable _portfolioFactory;
    IVotingEscrow public immutable _votingEscrow;
    IVoter public immutable _voter;

    event LockIncreased(uint256 indexed tokenId, uint256 amount, address indexed owner);
    event LockCreated(uint256 indexed tokenId, uint256 amount, address indexed owner);
    event LockMerged(uint256 indexed from, uint256 indexed to, uint256 weightIncrease, address indexed owner);

    error ListingActive(uint256 tokenId);

    constructor(address portfolioFactory, address votingEscrow, address voter) {
        require(portfolioFactory != address(0));
        require(votingEscrow != address(0), "Voting escrow address cannot be zero");
        _portfolioFactory = PortfolioFactory(portfolioFactory);
        _votingEscrow = IVotingEscrow(votingEscrow);
        _voter = IVoter(voter);
    }

    function increaseLock(uint256 tokenId, uint256 amount) external {
        // if msg.sender is portfolio manager, use the portfolio owner as the from address, otherwise use the caller
        address from = msg.sender == address(_portfolioFactory.portfolioManager()) ? _portfolioFactory.ownerOf(address(this)) : msg.sender;
        IERC20 votingEscrow = IERC20(_votingEscrow.token());
        votingEscrow.safeTransferFrom(from, address(this), amount);
        votingEscrow.approve(address(_votingEscrow), amount);
        _votingEscrow.increaseAmount(tokenId, amount);
        CollateralManager.updateLockedCollateral(address(_portfolioFactory.portfolioFactoryConfig()), tokenId, address(_votingEscrow));
        emit LockIncreased(tokenId, amount, from);
    }

    function createLock(uint256 amount) external returns (uint256 tokenId) {
        // if msg.sender is portfolio manager, use the portfolio owner as the from address, otherwise use the caller
        address from = msg.sender == address(_portfolioFactory.portfolioManager()) ? _portfolioFactory.ownerOf(address(this)) : msg.sender;
        IERC20 votingEscrow = IERC20(_votingEscrow.token());
        votingEscrow.safeTransferFrom(from, address(this), amount);
        votingEscrow.approve(address(_votingEscrow), amount);
        tokenId = _votingEscrow.createLock(amount, 4 *365 days);
        CollateralManager.addLockedCollateral(address(_portfolioFactory.portfolioFactoryConfig()), tokenId, address(_votingEscrow));
        emit LockCreated(tokenId, amount, from);
    }

    function onERC721Received(address, address, uint256 tokenId, bytes calldata) external returns (bytes4) {
        if (msg.sender == address(_votingEscrow)) {
            CollateralManager.addLockedCollateral(address(_portfolioFactory.portfolioFactoryConfig()), tokenId, address(_votingEscrow));
        }
        return IERC721Receiver.onERC721Received.selector;
    }

    function merge(uint256 fromToken, uint256 toToken) external {
        require(_votingEscrow.ownerOf(toToken) == address(this));
        require(_votingEscrow.ownerOf(fromToken) != address(this));
        address config = address(_portfolioFactory.portfolioFactoryConfig());
        address owner = _portfolioFactory.ownerOf(address(this));

        // Ensure toToken is tracked as collateral so updateLockedCollateral isn't a no-op
        CollateralManager.addLockedCollateral(config, toToken, address(_votingEscrow));

        int128 beginningBalance = _votingEscrow.locked(toToken).amount;
        _votingEscrow.merge(fromToken, toToken);
        int128 weightIncrease = _votingEscrow.locked(toToken).amount - beginningBalance;
        require(weightIncrease >= 0, "Negative weight increase");

        CollateralManager.updateLockedCollateral(config, toToken, address(_votingEscrow));
        emit LockMerged(fromToken, toToken, uint256(uint128(weightIncrease)), owner);
    }

    function mergeInternal(uint256 fromToken, uint256 toToken) external onlyPortfolioManagerMulticall(_portfolioFactory) {
        require(fromToken != toToken, "SameNFT");
        require(_votingEscrow.ownerOf(fromToken) == address(this), "from not in account");
        require(_votingEscrow.ownerOf(toToken) == address(this), "to not in account");
        if (UserMarketplaceModule.hasSaleAuthorization(fromToken)) {
            revert ListingActive(fromToken);
        }
        if (UserMarketplaceModule.hasSaleAuthorization(toToken)) {
            revert ListingActive(toToken);
        }

        address config = address(_portfolioFactory.portfolioFactoryConfig());
        address owner = _portfolioFactory.ownerOf(address(this));

        // Ensure toToken is tracked as collateral so updateLockedCollateral isn't a no-op
        CollateralManager.addLockedCollateral(config, toToken, address(_votingEscrow));

        // Reset votes and unlock permanent on fromToken so VE merge doesn't revert
        _voter.reset(fromToken);
        _votingEscrow.unlockPermanent(fromToken);

        int128 beginningBalance = _votingEscrow.locked(toToken).amount;
        _votingEscrow.merge(fromToken, toToken);
        int128 weightIncrease = _votingEscrow.locked(toToken).amount - beginningBalance;
        require(weightIncrease >= 0, "Negative weight increase");

        // Update collateral: remove burned token, refresh survivor
        CollateralManager.removeLockedCollateral(fromToken, config, address(_votingEscrow));
        CollateralManager.updateLockedCollateral(config, toToken, address(_votingEscrow));

        emit LockMerged(fromToken, toToken, uint256(uint128(weightIncrease)), owner);
    }

}
