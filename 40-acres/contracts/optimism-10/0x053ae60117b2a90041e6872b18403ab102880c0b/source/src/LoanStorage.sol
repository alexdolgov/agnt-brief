// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.20;

import {Ownable2StepUpgradeable} from "@openzeppelin/contracts-upgradeable/access/Ownable2StepUpgradeable.sol";

abstract contract LoanStorage is Ownable2StepUpgradeable {
    /// @custom:storage-location erc7201:storage:LoanStorage
    struct LoanStorageStruct {
        uint256 _totalWeights;
        uint256 _managedNft;
        mapping(address => bool) _isApprovedToken; // approved tokens for loan contract
    }


    // keccak256(abi.encode(uint256(keccak256("erc7201:storage:LoanStorage")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant LoanStorageLocation = 0x6dd8e034303aa35aaf3d00b596a4336c7daf449d21fe7bc9975d763227647b00;

    function _getLoanStorage() private pure returns (LoanStorageStruct storage $) {
        assembly {
            $.slot := LoanStorageLocation
        }
    }

    /* Rate Methods */

    /// @dev Get the total weight for the loan contract
    function getTotalWeight() public view virtual returns (uint256) {
        LoanStorageStruct storage $ = _getLoanStorage();
        return $._totalWeights;

    }

    /// @dev Add total weight for the loan contract
    function addTotalWeight(uint256 weights) internal  {
        LoanStorageStruct storage $ = _getLoanStorage();
        $._totalWeights += weights;
    }

    /// @dev Subtract total weight for the loan contract
    function subTotalWeight(uint256 weights) internal {
        LoanStorageStruct storage $ = _getLoanStorage();
        $._totalWeights -= weights;
    }

    /// @dev Set the managed NFT for the loan contract
    function setManagedNft(uint256 managedNft) onlyOwner public virtual {
        LoanStorageStruct storage $ = _getLoanStorage();
        $._managedNft = managedNft;
    }

    /// @dev Get the managed NFT for the loan contract
    function getManagedNft() public view virtual returns (uint256) {
        LoanStorageStruct storage $ = _getLoanStorage();
        return $._managedNft;
    }

    /// @dev Check if the token is approved for the loan contract
    function isApprovedToken(address token) public view virtual returns (bool) {
        LoanStorageStruct storage $ = _getLoanStorage();
        return $._isApprovedToken[token];
    }
    /// @dev Set approved token for the loan contract
    function setApprovedToken(address token, bool approved) public onlyOwner virtual {
        LoanStorageStruct storage $ = _getLoanStorage();
        $._isApprovedToken[token] = approved;
    }
}