// SPDX-License-Identifier: MIT

pragma solidity ^0.8.28;

import "openzeppelin-contracts-upgradeable/contracts/utils/cryptography/SignatureCheckerUpgradeable.sol";
import "./libraries/EIP7598Constants.sol";
import "./Stablecoin.sol";

contract StablecoinV2 is Stablecoin {

    using ECDSAUpgradeable for bytes32;
    using AddressUpgradeable for address;

    // ============ Storage Layout ============
    // Slot 359: _authorizationStates (mapping, 1 slot for the mapping pointer)
    // Slot 360: eip7598EnableFlag (bool, 1 byte, packed in slot 360)
    // Slot 361-408: __gap (48 slots reserved for future upgrades)
    // ========================================

    // State variable for tracking used authorization nonces
    // This uses bytes32 nonces to allow for flexible nonce strategies
    mapping(address => mapping(bytes32 => bool)) private _authorizationStates;
    
    bool public eip7598EnableFlag;

    // Events
    event AuthorizationUsed(address indexed authorizer, bytes32 indexed nonce);
    event AuthorizationCanceled(address indexed authorizer, bytes32 indexed nonce);
    event EIP7598Enabled();
    event EIP7598Disabled();

    /**
     * @dev Throws if eip7598 is disabled.
     */
    modifier eip7598Enabled() {
       require(eip7598EnableFlag, "EIP7598 is disabled");
        _;
    }

    /**
     * @dev Disable initializers for the implementation contract
     */
    constructor() {
        _disableInitializers();
    }

    /**
     * @dev Initialize the contract for V2 upgrade
     */   
    function initializeV2() public reinitializer(2) {  
        eip7598EnableFlag = true;
    }

    /**
    *  @dev enable eip7598 support
     * Can only be called by the owner.
     */
    function enableEIP7598() external onlyOwner {
        emit EIP7598Enabled();
        eip7598EnableFlag = true;
    }

    /**
    *  @dev disable eip7598 support
     * Can only be called by the owner.
     */
    function disableEIP7598() external onlyOwner {
        emit EIP7598Disabled();
        eip7598EnableFlag = false;
    }

    /**
     * @dev Execute a transfer with an authorization signature, backward-compatible with the ERC-3009 standard.
     * @param from Payer's address (Authorizer)
     * @param to Payee's address
     * @param value Amount to transfer
     * @param validAfter The time after which this is valid (unix time)
     * @param validBefore The time before which this is valid (unix time)
     * @param nonce Unique nonce for this authorization
     * @param signature Signature bytes (EOA signature or EIP-1271 contract signature)
     */
    function transferWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        bytes memory signature
    ) external eip7598Enabled {
        _transferOrReceiveWithAuthorization(EIP7598Constants.TRANSFER_WITH_AUTHORIZATION_TYPEHASH, from, to, value, validAfter, validBefore, nonce, signature);
    }

    /**
     * @dev Execute a transfer with an authorization signature (EIP-7598)
     * @param from Payer's address (Authorizer)
     * @param to Payee's address
     * @param value Amount to transfer
     * @param validAfter The time after which this is valid (unix time)
     * @param validBefore The time before which this is valid (unix time)
     * @param nonce Unique nonce for this authorization
     * @param v Signature bytes (EOA signature or EIP-1271 contract signature)
     * @param r Signature bytes (EOA signature or EIP-1271 contract signature)
     * @param s Signature bytes (EOA signature or EIP-1271 contract signature)
     */
    function transferWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external eip7598Enabled {
        _transferOrReceiveWithAuthorization(EIP7598Constants.TRANSFER_WITH_AUTHORIZATION_TYPEHASH, from, to, value, validAfter, validBefore, nonce, abi.encodePacked(r, s, v));
    }

    /**
    * @notice Receive a transfer with a signed authorization from the payer
    * @dev This has an additional check to ensure that the payee's address matches
    * the caller of this function to prevent front-running attacks. (See security
    * considerations)
    * @param from          Payer's address (Authorizer)
    * @param to            Payee's address
    * @param value         Amount to be transferred
    * @param validAfter    The time after which this is valid (unix time)
    * @param validBefore   The time before which this is valid (unix time)
    * @param nonce         Unique nonce
    * @param v Signature bytes (EOA signature or EIP-1271 contract signature)
    * @param r Signature bytes (EOA signature or EIP-1271 contract signature)
    * @param s Signature bytes (EOA signature or EIP-1271 contract signature)
    */
    function receiveWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        uint8 v, 
        bytes32 r, 
        bytes32 s
    ) external eip7598Enabled {
        require(msg.sender == to, "Caller must be the payee");
        _transferOrReceiveWithAuthorization(EIP7598Constants.RECEIVE_WITH_AUTHORIZATION_TYPEHASH, from, to, value, validAfter, validBefore, nonce, abi.encodePacked(r, s, v));
    }

     /**
    * @notice Receive a transfer with a signed authorization from the payer
    * @dev This has an additional check to ensure that the payee's address matches
    * the caller of this function to prevent front-running attacks. (See security
    * considerations)
    * @param from          Payer's address (Authorizer)
    * @param to            Payee's address
    * @param value         Amount to be transferred
    * @param validAfter    The time after which this is valid (unix time)
    * @param validBefore   The time before which this is valid (unix time)
    * @param nonce         Unique nonce
    * @param signature     Unstructured bytes signature signed by an EOA wallet or a contract wallet
    */
    function receiveWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        bytes memory signature
    ) external eip7598Enabled {
        require(msg.sender == to, "Caller must be the payee");
        _transferOrReceiveWithAuthorization(EIP7598Constants.RECEIVE_WITH_AUTHORIZATION_TYPEHASH, from, to, value, validAfter, validBefore, nonce, signature);
    }


     /**
     * @dev Check if an authorization has been used
     * @param authorizer Address that provided the authorization
     * @param nonce Nonce of the authorization
     * @return True if the authorization has been used
     */
    function authorizationState(address authorizer, bytes32 nonce) external view returns (bool) {
        return _authorizationStates[authorizer][nonce];
    }

    /**
     * @dev Execute a transfer with an authorization signature (EIP-7598)
     * @param from Payer's address (Authorizer)
     * @param to Payee's address
     * @param value Amount to transfer
     * @param validAfter The time after which this is valid (unix time)
     * @param validBefore The time before which this is valid (unix time)
     * @param nonce Unique nonce for this authorization
     * @param signature Signature bytes (EOA signature or EIP-1271 contract signature)
     */
    function _transferOrReceiveWithAuthorization(
        bytes32 typeHash,
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        bytes memory signature
    ) internal {
        // Validate time window
        require(block.timestamp > validAfter, "Authorization not yet valid");
        require(block.timestamp < validBefore, "Authorization expired");

        // Validate nonce
        require(!_authorizationStates[from][nonce], "Authorization already used");

        // Build EIP-712 struct hash
        bytes32 structHash = keccak256(
            abi.encode(
                typeHash,
                from,
                to,
                value,
                validAfter,
                validBefore,
                nonce
            )
        );

        // Build EIP-712 digest
        bytes32 digest = _hashTypedDataV4(structHash);

        // Validate signature (supports both EOA and EIP-1271 smart contracts)
        require(
            SignatureCheckerUpgradeable.isValidSignatureNow(from, digest, signature),
            "Invalid signature"
        );

        // Mark authorization as used
        _authorizationStates[from][nonce] = true;
        emit AuthorizationUsed(from, nonce);

        // Execute the transfer
        _transfer(from, to, value);
    }

    /**
     * @dev Cancel an authorization before it's used
     * @param authorizer Address that provided the authorization (must be msg.sender)
     * @param nonce Nonce of the authorization to cancel
     */
    function cancelAuthorization(address authorizer, bytes32 nonce) external {
        require(msg.sender == authorizer, "Caller must be the authorizer");
        require(!_authorizationStates[authorizer][nonce], "Authorization already used");

        _authorizationStates[authorizer][nonce] = true;
        emit AuthorizationCanceled(authorizer, nonce);
    }

    /**
     * @dev Gap for future upgrades
     * 
     * Storage Layout for StablecoinV2:
     * - Slot 359: _authorizationStates (mapping)
     * - Slot 360: eip7598EnableFlag (bool)
     * - Slot 361-408: __gap (48 slots)
     * 
     * Total new slots used: 2
     * Gap size: 48 (50 - 2 = 48)
     * 
     * IMPORTANT: When adding new state variables in future upgrades (V3, V4, etc.):
     * 1. Add new variables BEFORE this __gap
     * 2. Reduce __gap size by the number of new slots used
     * 3. Verify storage layout with `forge inspect StablecoinV3 storage-layout`
     */
    uint256[48] private __gap;
}
