// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.28;

import {IUltraVault} from "src/interfaces/IUltraVault.sol";
import {IUltraFrontendHelper } from "src/interfaces/IUltraFrontendHelper.sol";
import {IUltraVaultRateProvider} from "src/interfaces/IUltraVaultRateProvider.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {PendingRedeem} from "src/interfaces/IRedeemAccounting.sol";

contract UltraFrontendHelper is IUltraFrontendHelper {
    /////////////
    // Storage //
    /////////////

    IUltraVault internal immutable vault;

    //////////
    // Init //
    //////////

    constructor(address _vault) {
        require(_vault != address(0), "ZERO_ADDRESS");
        vault = IUltraVault(_vault);
        require(vault.fundsHolder() != address(0), "INVALID_VAULT");
    }

    ////////////////////
    // View Functions //
    ////////////////////

    function getVault() external view returns (address) {
        return address(vault);
    }
    
    /// @inheritdoc IUltraFrontendHelper
    function getVaultState() external view returns (VaultState memory) {
        uint8 decimals = vault.decimals();
        return VaultState({
            totalAssets: vault.totalAssets(),
            totalShares: vault.totalSupply(),
            sharePrice: vault.convertToAssets(10 ** decimals)
        });
    }

    /// @inheritdoc IUltraFrontendHelper
    function previewDeposit(address asset, uint256 amount) external view returns (uint256) {
        uint256 baseAssets = vault.convertToUnderlying(asset, amount);
        uint256 shares = vault.convertToShares(baseAssets);
        return shares;
    }

    /// @inheritdoc IUltraFrontendHelper
    function previewRedeem(address asset, uint256 shares) external view returns (uint256) {
        uint256 baseAssets = vault.convertToAssets(shares);
        uint256 assets = vault.convertFromUnderlying(asset, baseAssets);
        uint256 withdrawalFee = vault.calculateWithdrawalFee(assets);
        return assets - withdrawalFee;
    }

    /// @inheritdoc IUltraFrontendHelper
    function collectPendingRedeems(address[] calldata assets, address[] calldata users) external view returns (Redeem[] memory redeems) {
        // 1. Validate assets
        IUltraVaultRateProvider rateProvider = IUltraVaultRateProvider(vault.rateProvider());
        for (uint256 i = 0; i < assets.length; i++) {
            address asset = assets[i];
            require(rateProvider.isSupported(asset), "UNSUPPORTED_ASSET");
        }
        // 2. Count number of redeems
        uint256 totalRedeems = 0;
        for (uint256 i = 0; i < users.length; i++) {
            address user = users[i];
            for (uint256 j = 0; j < assets.length; j++) {
                uint256 shares = vault.getPendingRedeemForAsset(assets[j], user).shares;
                if (shares > 0) {
                    totalRedeems++;
                }
            }
        }
        if (totalRedeems == 0) {
            return redeems;
        }
        // 3. Build the response with redeems
        redeems = new Redeem[](totalRedeems);
        uint256 index = 0;
        for (uint256 i = 0; i < users.length; i++) {
            address user = users[i];
            for (uint256 j = 0; j < assets.length; j++) {
                address asset = assets[j];
                PendingRedeem memory redeem = vault.getPendingRedeemForAsset(asset, user);
                if (redeem.shares > 0) {
                    redeems[index] = Redeem({ 
                        user: user, 
                        asset: asset, 
                        shares: redeem.shares, 
                        requestTime: redeem.requestTime 
                    });
                    index++;
                }
            }
        }
        return redeems;
    }
}
