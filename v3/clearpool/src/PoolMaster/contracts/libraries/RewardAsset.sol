// SPDX-License-Identifier: MIT
pragma solidity 0.8.19;

import {SafeCastUpgradeable} from '@openzeppelin/contracts-upgradeable/utils/math/SafeCastUpgradeable.sol';

library RewardAsset {
  using SafeCastUpgradeable for uint256;
  using SafeCastUpgradeable for int256;

  struct RewardAssetData {
    address asset;
    uint256 rate;
  }

  struct Data {
    mapping(address => bool) flags;
    mapping(address => uint256) addressIndex;
    mapping(address => uint256) magnifiedRewardPerShare; // rewardAsset -> magnifiedRewardPerShare
    mapping(uint256 => uint256) roundLastDistribution; //Timestamp when last round staking reward distribution occured
    mapping(address => mapping(address => int256)) magnifiedRewardCorrections; // rewardAsset -> account -> magnifiedReward correction
    mapping(address => mapping(address => uint256)) rewardWithdrawals; // rewardAsset -> lender -> amount
    mapping(uint256 => mapping(address => uint256)) magnifiedRoundRewardPerShare; // round bond ID -> rewardAsset -> magnifiedRewardPerShare
    RewardAssetData[] rewardAssetData;
    uint256 id;
    uint256 lastRewardDistribution; //Timestamp when last staking reward distribution occured
  }

  function insert(Data storage self, address asset, uint256 rate) internal returns (bool) {
    if (self.flags[asset]) {
      uint256 index = self.addressIndex[asset];

      if (self.rewardAssetData[index].rate == rate) {
        return false;
      }
      self.rewardAssetData[index].rate = rate;
      return true;
    }

    self.flags[asset] = true;
    self.rewardAssetData.push(RewardAssetData(asset, rate));
    self.addressIndex[asset] = self.id;
    self.id++;
    return true;
  }

  function decreaseRewardCorrection(
    Data storage self,
    address asset,
    address account,
    uint256 amount
  ) internal {
    return
      decreaseRewardCorrection(self, asset, account, amount, self.magnifiedRewardPerShare[asset]);
  }

  function decreaseRewardCorrection(
    Data storage self,
    address asset,
    address account,
    uint256 amount,
    uint256 share
  ) internal {
    self.magnifiedRewardCorrections[asset][account] -= (share * amount).toInt256();
  }

  function increaseRewardCorrection(
    Data storage self,
    address asset,
    address account,
    uint256 amount
  ) internal {
    return
      increaseRewardCorrection(self, asset, account, amount, self.magnifiedRewardPerShare[asset]);
  }

  function increaseRewardCorrection(
    Data storage self,
    address asset,
    address account,
    uint256 amount,
    uint256 share
  ) internal {
    self.magnifiedRewardCorrections[asset][account] += (share * amount).toInt256();
  }

  function updateMagnifiedRewardPerShare(
    Data storage self,
    address asset,
    uint256 period,
    uint256 rate,
    uint256 rewardMagnitude,
    uint256 totalSupply
  ) internal {
    uint256 _rewardPerShare = ((rewardMagnitude * period * rate)) / totalSupply;
    self.magnifiedRewardPerShare[asset] += _rewardPerShare;
  }

  function updateMagnifiedRoundRewardPerShare(
    Data storage self,
    address asset,
    uint256 roundBondId,
    uint256 period,
    uint256 rate,
    uint256 rewardMagnitude,
    uint256 totalSupply
  ) internal {
    uint256 _rewardPerShare = ((rewardMagnitude * period * rate)) / totalSupply;
    self.magnifiedRoundRewardPerShare[roundBondId][asset] += _rewardPerShare;
  }

  function updateRewardWithdrawals(
    Data storage self,
    address asset,
    address account,
    uint256 amount
  ) internal {
    self.rewardWithdrawals[asset][account] += amount;
  }

  function getList(Data storage self) internal view returns (RewardAssetData[] memory) {
    return self.rewardAssetData;
  }
}
