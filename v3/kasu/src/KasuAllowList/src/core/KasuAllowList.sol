// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.23;

import "NexeraIDSigGatingContracts/TxAuthDataVerifierUpgradeable.sol";
import "./interfaces/IKasuAllowList.sol";
import "./interfaces/lendingPool/ILendingPoolErrors.sol";
import "../shared/access/KasuAccessControllable.sol";
import "../shared/AddressLib.sol";

/**
 * @title Kasu Allow List Contract
 * @notice This contract is used to verify if users are allowed to interact with the protocol.
 */
contract KasuAllowList is IKasuAllowList, KasuAccessControllable, TxAuthDataVerifierUpgradeable {
    /// @notice Lending Pool Manager address.
    address private _lendingPoolManager;

    /**
     * @notice Constructor.
     * @param kasuController_ Kasu controller contract.
     */
    constructor(IKasuController kasuController_) KasuAccessControllable(kasuController_) {
        _disableInitializers();
    }

    /// @notice Manual allow list of users.
    mapping(address => bool) public allowList;

    /// @notice Block list of users.
    /// @dev If a user is in the block list, it will be blocked even if it is in the allow list or KYCd.
    mapping(address => bool) public blockList;

    /* ========== INITIALIZER ========== */

    /**
     * @notice Initialize the contract.
     * @param lendingPoolManager_ Lending Pool Manager address.
     * @param signer_ The address of the user KYC data signer.
     */
    function initialize(address lendingPoolManager_, address signer_) public initializer {
        AddressLib.checkIfZero(lendingPoolManager_);
        AddressLib.checkIfZero(signer_);

        _lendingPoolManager = lendingPoolManager_;
        __TxAuthDataVerifierUpgradeable_init(signer_);
    }

    /* ========== EXTERNAL MUTATIVE FUNCTIONS ========== */

    /**
     * @notice Sets the Nexera ID signer address.
     * @dev Can only be called by the admin.
     * @param signer_ The address of the user KYC data signer.
     */
    function setNexeraIDSigner(address signer_) external onlyAdmin {
        AddressLib.checkIfZero(signer_);
        _setSigner(signer_);
    }

    /**
     * @notice Manually allow a user to interact with the protocol.
     * @dev Can only be called by the admin.
     * @param user The user's address.
     */
    function allowUser(address user) external onlyAdmin {
        AddressLib.checkIfZero(user);

        if (!allowList[user]) {
            allowList[user] = true;
            emit IKasuAllowList.UserAddedInAllowList(user);
        }
    }

    /**
     * @notice Remove allowance of a user to interact with the protocol.
     * @dev Can only be called by the admin.
     * @param user The user's address.
     */
    function disallowUser(address user) external onlyAdmin {
        AddressLib.checkIfZero(user);

        if (allowList[user]) {
            allowList[user] = false;
            emit IKasuAllowList.UserRemovedFromAllowList(user);
        }
    }

    /**
     * @notice Block a user from interacting with the protocol.
     * @dev If blocked, the user will not be able to interact with the protocol even if it is in the allow list or KYCd.
     * Can only be called by the admin.
     * @param user The user's address.
     */
    function blockUser(address user) external onlyAdmin {
        AddressLib.checkIfZero(user);

        if (!blockList[user]) {
            blockList[user] = true;
            emit IKasuAllowList.UserBlockedFromAllowList(user);
        }
    }

    /**
     * @notice Remove user from a block list.
     * @dev Can only be called by the admin.
     * @param user The user's address.
     */
    function unblockUser(address user) external onlyAdmin {
        AddressLib.checkIfZero(user);

        if (blockList[user]) {
            blockList[user] = false;
            emit IKasuAllowList.UserUnblockedFromAllowList(user);
        }
    }

    /**
     * @notice Verifies the user's KYC status via Nexera ID.
     * @dev Can only be called by the Lending Pool Manager as verifying increments the nonce of the user.
     * The calldata must contain the block expiration number and the signature of the KYC data.
     * KYC data is not part of the function parameters, but it's extracted from the end of the calldata when verifying the KYC.
     * @param user The user's address.
     * @return A boolean indicating whether the user KYC is verified.
     */
    function verifyUserKyc(address user) external returns (bool) {
        if (msg.sender != _lendingPoolManager) {
            revert ILendingPoolErrors.OnlyLendingPoolManager();
        }

        return _verifyTxAuthData(_msgData(), user);
    }
}
