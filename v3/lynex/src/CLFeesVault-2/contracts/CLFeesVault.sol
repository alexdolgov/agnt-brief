// SPDX-License-Identifier: MIT
pragma solidity 0.8.13;

import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";


import "./interfaces/IPairInfo.sol";
import "./interfaces/IBribe.sol";
import "./interfaces/IVoter.sol";
import "./interfaces/IPermissionsRegistry.sol";
import "./interfaces/IPairFactory.sol";
import "./libraries/Math.sol";

/**
 * @title CLFeesVault ("GammaFeesVault" would be more appropriate)
 * @dev This contract is responsible for managing and distributing fees generated from a liquidity pool in a DeFi application.
 * It supports fee distribution to different roles such as gamma, referral, and gauge. The contract also provides administrative
 * functions for setting various parameters like referral share, gamma share, gamma recipient, dibs, voter, permissions registry, and pool.
 * It uses role-based access control, leveraging a permissions registry contract to restrict access to certain functions.
 * Additionally, it provides an emergency recovery function to recover any ERC20 tokens accidentally sent to the contract.
 * The contract is designed to work with a GAMMA-like Automated Liquidity Management (ALM) service.
 */
contract CLFeesVault is Initializable {
    using SafeERC20 for IERC20;

    /* -----------------------------------------------------------------------------
                                    DATA
    ----------------------------------------------------------------------------- */
    struct FeeData {
        uint256 amount0;
        uint256 amount1;
    }

    bool public constant isFeeVault = true;
    uint256 public referralShare = 0;
    uint256 public constant REFERRAL_MAX = 1000; //10%

    IVoter public voter;

    uint256 public constant PRECISION = 10000;
    uint256 public gammaShare = 0; // usually 7%
    uint256 public constant GAMMA_MAX = 2500; //25%

    address public pool;
    address public gammaRecipient;
    address public dibs;
    address public pairFactoryClassic;
    IPermissionsRegistry public permissionsRegistry;

    /* -----------------------------------------------------------------------------
                                    MODIFIERS
    ----------------------------------------------------------------------------- */
    modifier onlyGauge() {
        require(voter.isGauge(msg.sender), "!gauge contract");
        _;
    }
    modifier onlyAdmin() {
        require(permissionsRegistry.hasRole("CL_FEES_VAULT_ADMIN", msg.sender), "ERR: GAUGE_ADMIN");
        _;
    }

    /* -----------------------------------------------------------------------------
                                    EVENTS
    ----------------------------------------------------------------------------- */
    event Fees(uint256 totAmount0,uint256 totAmount1, address indexed token0, address indexed token1, address indexed pool, uint timestamp);
    event Fees0(uint gamma, uint referral, uint gauge, address indexed token);
    event Fees1(uint gamma, uint referral, uint gauge, address indexed token);
    event SetReferralShare(uint256 share);

    /* -----------------------------------------------------------------------------
                                    CONSTRUCTOR AND INIT
    ----------------------------------------------------------------------------- */
    function initialize(address _pool, address _permissionRegistry, address _voter, address _gammaFeeRecipient, address _pairFactoryClassic) public initializer {
        permissionsRegistry = IPermissionsRegistry(_permissionRegistry);
        pool = _pool;
        voter = IVoter(_voter);
        gammaRecipient = _gammaFeeRecipient;
        pairFactoryClassic = _pairFactoryClassic;
        if (_pairFactoryClassic != address(0)) {
            dibs = IPairFactory(pairFactoryClassic).dibs();
        }
    }

    /* -----------------------------------------------------------------------------
    --------------------------------------------------------------------------------
    --------------------------------------------------------------------------------
                                    LP FEES CLAIM
    --------------------------------------------------------------------------------
    --------------------------------------------------------------------------------
    ----------------------------------------------------------------------------- */

    /// @dev    Claim Fees from the gauge. Return the fees claimed by the gauge
    function claimFees() external onlyGauge returns(uint256 gauge0, uint256 gauge1) {
        // check gauge pool using voter
        address _pool = voter.poolForGauge(msg.sender);
        require(pool == _pool, "invalid pool");

        // fees
        uint gamma;
        uint referral;

        // token0
        address t0 = IPairInfo(pool).token0();
        uint256 _amount0 = IERC20(t0).balanceOf(address(this));

    
        if(_amount0 > 0){
            (gamma, referral, gauge0) = _getFees(_amount0);
            if(gauge0 > 0) IERC20(t0).safeTransfer(msg.sender, gauge0);
            if(gamma > 0) IERC20(t0).safeTransfer(gammaRecipient, gamma);
            if(referral > 0 && dibs != address(0)) IERC20(t0).safeTransfer(dibs, referral);
            emit Fees0(gamma, referral, gauge0, t0);
        }
        // token1
        address t1 = IPairInfo(pool).token1();
        uint256 _amount1 = IERC20(t1).balanceOf(address(this));

        if(_amount1 > 0){
            (gamma, referral, gauge1) = _getFees(_amount1); 
            if(gauge1 > 0) IERC20(t1).safeTransfer(msg.sender, gauge1);
            if(gamma > 0) IERC20(t1).safeTransfer(gammaRecipient, gamma);
            if(referral > 0 && dibs != address(0)) IERC20(t1).safeTransfer(dibs, referral);
            emit Fees1(gamma, referral, gauge1, t1);
        }

        emit Fees(_amount0, _amount1, t0, t1, pool, block.timestamp);  
    }

    function _getFees(uint amount) internal view returns(uint gamma, uint referral, uint gauge) { 
        uint referralFee = pairFactoryClassic != address(0) 
            ? IPairFactory(pairFactoryClassic).MAX_REFERRAL_FEE() 
            : referralShare;
        referral = amount * referralFee / PRECISION;
        gamma = amount * gammaShare / PRECISION;
        gauge = amount - gamma - referral;
    }
    
    /* -----------------------------------------------------------------------------
    --------------------------------------------------------------------------------
    --------------------------------------------------------------------------------
                                    ADMIN FUNCTIONS
    --------------------------------------------------------------------------------
    --------------------------------------------------------------------------------
    ----------------------------------------------------------------------------- */
    function setReferralShare(uint256 share) external onlyAdmin {
        require(share <= REFERRAL_MAX);
        referralShare = share;
        emit SetReferralShare(share);
    }

    function setGammaShare(uint256 share) external onlyAdmin {
        require(share <= GAMMA_MAX);
        gammaShare = share;
    }

    function setGammaRecipient(address _gR) external onlyAdmin {
        require(_gR != address(0));
        gammaRecipient = _gR;
    }

    function setDibs(address _dibs) external onlyAdmin {
        require(_dibs != address(0));
        dibs = _dibs;
    }

    function setPairFactory(address _pf) external onlyAdmin {
        pairFactoryClassic = _pf;
    }

    function setVoter(address vt) external onlyAdmin {
        require(vt != address(0));
        voter = IVoter(vt);
    }

    function setPermissionRegistry(address _pr) external onlyAdmin {
        require(_pr != address(0));
        permissionsRegistry = IPermissionsRegistry(_pr);
    }

    function setPool(address _pool) external onlyAdmin {
        require(_pool != address(0));
        pool = _pool;
    }

    /// @notice Recover ERC20 from the contract.
    function emergencyRecoverERC20(address tokenAddress, uint256 tokenAmount) external onlyAdmin {
        require(tokenAmount <= IERC20(tokenAddress).balanceOf(address(this)));
        IERC20(tokenAddress).safeTransfer(permissionsRegistry.lynexTeamMultisig(), tokenAmount);
    }
}
