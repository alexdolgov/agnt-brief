// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {EnumerableSet} from "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";
import {ERC20Upgradeable} from "@openzeppelin/contracts-upgradeable/token/ERC20/ERC20Upgradeable.sol";
import {UUPSUpgradeable} from "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";

import {currentEpoch, WEEK} from "src/libraries/EpochMath.sol";

error Abra_NotMinter(address sender);
error Abra_MaxMintersReached();

contract Abra is ERC20Upgradeable, OwnableUpgradeable, UUPSUpgradeable {
    using EnumerableSet for EnumerableSet.AddressSet;

    uint public immutable MAX_MINTERS;

    mapping(uint32 epoch => uint112) public supplyChekpoints;
    EnumerableSet.AddressSet private minters; 

    constructor(uint maxMinters) {
        MAX_MINTERS = maxMinters;
    }

    function initialize(uint _initialSupply) public initializer {
        __ERC20_init("schwABRA Finance", "schwABRA");
        __Ownable_init_unchained(msg.sender);
        __UUPSUpgradeable_init();
        _mint(_msgSender(), _initialSupply);
        supplyChekpoints[currentEpoch()] = uint112(_initialSupply);
    }

    function _authorizeUpgrade(address newImplementation) internal override onlyOwner {}


    function mint(address account, uint amount) external {
        if (!minters.contains(msg.sender)) {
            revert Abra_NotMinter(msg.sender);
        }
        _mint(account, amount);
        supplyChekpoints[currentEpoch()] = uint112(totalSupply()); // uint112 should be enough for ABRA
    }

    function burn(uint amount) external {
        _burn(msg.sender, amount);
        supplyChekpoints[currentEpoch()] = uint112(totalSupply()); // uint112 should be enough for ABRA
    }

    // NOTE: This function must be timelocked
    function setMinter(address minter) external onlyOwner {
        if (minters.length() >= MAX_MINTERS) {
            revert Abra_MaxMintersReached();
        }
        minters.add(minter);
    }

    function removeMinter(address minter) external onlyOwner {
        minters.remove(minter);
    }

    function mintersLength() external view returns (uint) {
        return minters.length();
    }

    function minterAt(uint index) external view returns (address) {
        return minters.at(index);
    }

    function makeupEpoch(uint32 epoch) external onlyOwner {
        // make up for only past epochs
        if (epoch < currentEpoch() && supplyChekpoints[epoch] == 0) {
            uint32 prevEpoch = epoch - WEEK;
            supplyChekpoints[epoch] = supplyChekpoints[prevEpoch];             
        }
    }
}