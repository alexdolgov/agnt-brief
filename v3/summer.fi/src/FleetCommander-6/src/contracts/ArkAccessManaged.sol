// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.27;

import {IArkAccessManaged} from "../interfaces/IArkAccessManaged.sol";

import {IConfigurationManaged} from "../interfaces/IConfigurationManaged.sol";
import {IFleetCommander} from "../interfaces/IFleetCommander.sol";
import {ContractSpecificRoles} from "../interfaces/IProtocolAccessManager.sol";

import {ProtocolAccessManaged} from "./ProtocolAccessManaged.sol";

/**
 * @title ArkAccessManaged
 * @author SummerFi
 * @custom:see IArkAccessManaged
 */
contract ArkAccessManaged is IArkAccessManaged, ProtocolAccessManaged {
    /**
     * @param accessManager The access manager address
     */
    constructor(address accessManager) ProtocolAccessManaged(accessManager) {}

    /**
     * @dev Modifier to check that the caller has the appropriate role to board
     * @param commander The address of the FleetCommander
     * @custom:internal-logic
     * - Checks if the caller has the Commander role
     * - If not, checks if the caller is the RAFT contract
     * - If not, checks if the caller is an active Ark in the FleetCommander
     * @custom:effects
     * - Reverts if the caller doesn't have the necessary permissions
     * - Allows the function to proceed if the caller is authorized
     * @custom:security-considerations
     * - Ensures that only authorized entities can board funds
     * - Relies on the correct setup of the FleetCommander and RAFT contracts
     */
    modifier onlyAuthorizedToBoard(address commander) {
        if (!_hasCommanderRole()) {
            address msgSender = _msgSender();
            bool isRaft = msgSender ==
                IConfigurationManaged(address(this)).raft();

            if (!isRaft) {
                bool isArk = IFleetCommander(commander).isArkActive(msgSender);
                if (!isArk) {
                    revert CallerIsNotAuthorizedToBoard(msgSender);
                }
            }
        }
        _;
    }

    /**
     * @dev Modifier to check that the caller is the RAFT contract
     * @custom:internal-logic
     * - Retrieves the RAFT address from the ConfigurationManaged contract
     * - Compares the caller's address with the RAFT address
     * @custom:effects
     * - Reverts if the caller is not the RAFT contract
     * - Allows the function to proceed if the caller is the RAFT contract
     * @custom:security-considerations
     * - Ensures that only the RAFT contract can call certain functions
     * - Relies on the correct setup of the ConfigurationManaged contract
     */
    modifier onlyRaft() {
        if (_msgSender() != IConfigurationManaged(address(this)).raft()) {
            revert CallerIsNotRaft(_msgSender());
        }
        _;
    }

    /**
     * @dev Modifier to check that the caller has the Commander role
     * @custom:internal-logic
     * - Calls the internal _hasCommanderRole function to check the caller's role
     * @custom:effects
     * - Reverts if the caller doesn't have the Commander role
     * - Allows the function to proceed if the caller has the Commander role
     * @custom:security-considerations
     * - Ensures that only the designated Commander can call certain functions
     * - Relies on the correct setup of the access control system
     */
    modifier onlyCommander() {
        if (!_hasCommanderRole()) {
            revert CallerIsNotCommander(_msgSender());
        }
        _;
    }

    /**
     * @dev Internal function to check if the caller has the Commander role
     * @return bool True if the caller has the Commander role, false otherwise
     * @custom:internal-logic
     * - Generates the Commander role identifier for this contract
     * - Checks if the caller has the generated role in the access manager
     * @custom:effects
     * - Does not modify any state, view function only
     * @custom:security-considerations
     * - Relies on the correct setup of the access manager
     * - Assumes that the Commander role is properly assigned
     */
    function _hasCommanderRole() internal view returns (bool) {
        return
            _accessManager.hasRole(
                generateRole(
                    ContractSpecificRoles.COMMANDER_ROLE,
                    address(this)
                ),
                _msgSender()
            );
    }
}
