// SPDX-License-Identifier: AGPL-3.0
pragma solidity ^0.8.18;

import {BaseHealthCheck} from "@periphery/Bases/HealthCheck/BaseHealthCheck.sol";

/// @notice Base contract for cross-chain strategies on the origin chain
/// @dev Provides message ordering, remote asset tracking, and abstract bridging interface
abstract contract BaseCrossChain is BaseHealthCheck {
    event RemoteAssetsUpdated(uint256 indexed amount);

    /// @notice Address allowed to deposit into this strategy
    address public immutable DEPOSITER;

    /// @notice Remote chain identifier specific to bridge implementation.
    bytes32 public immutable REMOTE_ID;

    /// @notice Remote chain ID.
    uint256 public immutable REMOTE_CHAIN_ID;

    /// @notice Address of the remote strategy counterpart
    address public immutable REMOTE_COUNTERPART;

    /// @notice Tracks remote assets
    uint256 public remoteAssets;

    /// @notice Timestamp watermark for remote asset updates
    uint256 public lastRemoteAssetsReport;

    constructor(
        address _asset,
        string memory _name,
        bytes32 _remoteId,
        uint256 _remoteChainId,
        address _remoteCounterpart,
        address _depositer
    ) BaseHealthCheck(_asset, _name) {
        require(_remoteCounterpart != address(0), "ZeroAddress");
        require(_depositer != address(0), "ZeroAddress");
        // Note: _remoteId can be 0 for some chains (e.g., Ethereum domain = 0)

        REMOTE_ID = _remoteId;
        REMOTE_CHAIN_ID = _remoteChainId;
        REMOTE_COUNTERPART = _remoteCounterpart;
        DEPOSITER = _depositer;

        _setProfitLimitRatio(1_000); // 10%
    }

    /*//////////////////////////////////////////////////////////////
                        INTERNAL STRATEGY METHODS
    //////////////////////////////////////////////////////////////*/

    /// @notice Reports total assets including remote deployments
    /// @return _totalAssets Sum of local balance and remote assets
    function _harvestAndReport()
        internal
        virtual
        override
        returns (uint256 _totalAssets)
    {
        _totalAssets = balanceOfAsset() + remoteAssets;
    }

    /*//////////////////////////////////////////////////////////////
                    DEPOSIT/WITHDRAW LIMIT OVERRIDES
    //////////////////////////////////////////////////////////////*/

    /// @notice Only local balance is available for withdrawal
    /// @dev Remote assets must be bridged back before withdrawal
    function availableWithdrawLimit(
        address /* _owner */
    ) public view virtual override returns (uint256) {
        return balanceOfAsset();
    }

    /// @notice Restrict deposits to specific depositer address
    /// @dev Override to change deposit access control
    function availableDepositLimit(
        address _owner
    ) public view virtual override returns (uint256) {
        return _owner == DEPOSITER ? type(uint256).max : 0;
    }

    /// @notice Handles incoming cross-chain messages
    /// @param amount Total amount of assets the remote strategy has.
    /// @param timestamp Timestamp when the remote strategy sent the report.
    function _handleIncomingMessage(
        uint256 amount,
        uint256 timestamp
    ) internal virtual {
        if (timestamp <= lastRemoteAssetsReport) {
            return;
        }

        // NOTE: Reports older than the current watermark are ignored. Since
        // local deploys also update that watermark, a valid remote report can
        // still be skipped if it was sent before a later deposit. For example,
        // a withdrawal can trigger a lower remote asset report that arrives
        // after a new deposit already increased remoteAssets locally. In that
        // case accounting can remain incorrect until the next fresh remote
        // report replaces the local estimate.
        lastRemoteAssetsReport = timestamp;
        remoteAssets = amount;
        emit RemoteAssetsUpdated(amount);
    }

    function balanceOfAsset() public view virtual returns (uint256) {
        return asset.balanceOf(address(this));
    }

    /*//////////////////////////////////////////////////////////////
                        DEPLOYMENT LOGIC
    //////////////////////////////////////////////////////////////*/

    /// @notice Deploy funds to remote chain
    function _deployFunds(uint256 _amount) internal virtual override {
        uint256 newRemoteAssets = remoteAssets + _bridgeAssets(_amount);
        // Count deposits as remote assets immediately and advance the report
        // watermark so a report sent before this deploy cannot later overwrite
        // the locally-added amount before the remote side has received it.
        lastRemoteAssetsReport = block.timestamp;
        remoteAssets = newRemoteAssets;
        emit RemoteAssetsUpdated(newRemoteAssets);
    }

    /// @notice No-op for freeing funds (not needed for cross-chain strategies)
    function _freeFunds(uint256) internal pure virtual override {}

    /*//////////////////////////////////////////////////////////////
                    ABSTRACT METHODS TO IMPLEMENT
    //////////////////////////////////////////////////////////////*/

    /// @notice Bridge assets to remote chain
    /// @dev Implementation must handle bridge-specific token transfer logic
    /// @param amount Amount of tokens to bridge
    function _bridgeAssets(uint256 amount) internal virtual returns (uint256);
}
